plugins {
    id("eclipse_setting")
}

//==========================================================================
//Eclipse buildshipの設定
//==========================================================================

/**
 * プロジェクトがモジュールプロジェクトであるか判定する。
 * @return モジュールプロジェクトであれば true、そうでなければ false。
 */
boolean isModuleProject() {
    def isModule = false
    sourceSets.all { sourceSet ->
        if (sourceSet.hasProperty('java') && sourceSet.java.srcDirs) {
            def moduleInfo = sourceSet.java.srcDirs.find { dir ->
                new File(dir, 'module-info.java').exists()
            }
            if (moduleInfo) {
                isModule = true
                return
            }
        }
    }
    return isModule
}

afterEvaluate {
    def moduleProject = isModuleProject()
    println "Project is a module project: ${moduleProject}"

    eclipse.classpath.file.whenMerged { classpath ->
        if (moduleProject) {
            classpath.entries.findAll {
                it.properties.kind == 'lib' && it.entryAttributes['test'] != 'true'
            }.each {
                it.entryAttributes['module'] = 'true'
                println "marked module classpath entry: " + it.path
            }

            classpath.entries.findAll {
                it.properties.kind == 'lib' && it.entryAttributes['test'] == 'true'
            }.each {
                it.entryAttributes['module'] = 'false'
                println "unmarked classpath entry: " + it.path
            }

            classpath.entries.findAll {
                it.properties.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER')
            }.each {
                it.entryAttributes['module'] = 'true'
                println "marked module container: " + it.path
            }
        } else {
            classpath.entries.removeAll { entry ->
                if (entry.kind == "lib") {
                    println "delete classpath entry: " + entry.path
                    return true
                }
                return false
            }
        }
    }

    if (moduleProject) {
        // eclipse.classpath.containers "org.eclipse.jdt.launching.JRE_CONTAINER"
    } else {
        eclipse.classpath.containers "org.eclipse.buildship.core.gradleclasspathcontainer"
    }
}

eclipse {
    project {
        buildCommand "org.eclipse.buildship.core.gradleprojectbuilder"
        natures "org.eclipse.buildship.core.gradleprojectnature"
    }
}

import java.nio.file.Files

tasks.eclipse.doLast {
    File prefs = file(".settings/org.eclipse.buildship.core.prefs")
    if(!prefs.exists()) {
        String path = ""
        if(rootProject != this) {
            def root = rootProject.projectDir.toPath()
            def sub = projectDir.toPath()
            path = sub.relativize(root).toString()
        }
        Files.createDirectories(prefs.toPath().getParent())
        prefs.createNewFile()
        prefs.append("""
            connection.project.dir=$path
            eclipse.preferences.version=1
            """.stripIndent())
    }
}

tasks.cleanEclipse.doFirst {
    File prefs = file(".settings/org.eclipse.buildship.core.prefs")
    prefs.delete()
}